plugins {
	alias(libs.plugins.android.application)
	alias(libs.plugins.kotlin.android)
	alias(libs.plugins.kotlin.compose)
	alias(libs.plugins.kotlin.serialization)

	id("dagger.hilt.android.plugin")
	id("com.google.devtools.ksp")
}

fun gitOutput(vararg args: String): String? {
	return runCatching {
		val process = ProcessBuilder("git", *args)
			.directory(rootDir)
			.redirectErrorStream(true)
			.start()
		val output = process.inputStream.bufferedReader().readText().trim()
		if (process.waitFor() == 0) output.takeIf { it.isNotBlank() } else null
	}.getOrNull()
}

fun normalizedVersionTag(tag: String?): String? {
	val normalizedTag = tag?.removePrefix("refs/tags/") ?: return null
	return normalizedTag.takeIf { it.matches(Regex("^v?\\d+\\.\\d+\\.\\d+(-[A-Za-z0-9.-]+)?$")) }
}

fun releaseVersionName(): String {
	val tag = normalizedVersionTag(System.getenv("VERSION_TAG"))
		?: normalizedVersionTag(System.getenv("GITHUB_REF_NAME"))
		?: normalizedVersionTag(gitOutput("describe", "--tags", "--exact-match"))
		?: normalizedVersionTag(gitOutput("describe", "--tags", "--abbrev=0"))
		?: "v0.0.0-dev"

	return tag.removePrefix("v")
}

fun versionCodeFrom(versionName: String): Int {
	val parts = versionName.split("-", limit = 2).first()
		.split(".")
		.map { it.toIntOrNull() ?: 0 }
	val major = parts.getOrElse(0) { 0 }
	val minor = parts.getOrElse(1) { 0 }
	val patch = parts.getOrElse(2) { 0 }

	return major * 10_000 + minor * 100 + patch
}

val appVersionName = releaseVersionName()
val appVersionCode = versionCodeFrom(appVersionName)

android {
	namespace = "com.serranoie.app.minus"
	compileSdk {
		version = release(36)
	}

	val releaseStoreFile = System.getenv("MINUS_RELEASE_STORE_FILE")
	val hasReleaseSigningConfig = listOf(
		releaseStoreFile,
		System.getenv("MINUS_RELEASE_STORE_PASSWORD"),
		System.getenv("MINUS_RELEASE_KEY_ALIAS"),
		System.getenv("MINUS_RELEASE_KEY_PASSWORD")
	).all { !it.isNullOrBlank() }

	defaultConfig {
		applicationId = "com.serranoie.app.minus"
		minSdk = 27
		targetSdk = 36
		versionCode = appVersionCode
		versionName = appVersionName

		testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
	}

	signingConfigs {
		if (hasReleaseSigningConfig) {
			create("release") {
				storeFile = file(releaseStoreFile!!)
				storePassword = System.getenv("MINUS_RELEASE_STORE_PASSWORD")
				keyAlias = System.getenv("MINUS_RELEASE_KEY_ALIAS")
				keyPassword = System.getenv("MINUS_RELEASE_KEY_PASSWORD")
			}
		}
	}

	buildTypes {
		debug {
			buildConfigField("Boolean", "SHOW_LOGS", "true")
			buildConfigField("Boolean", "DEBUG_FEATURES", "true")
			isMinifyEnabled = false
		}
		release {
			buildConfigField("Boolean", "SHOW_LOGS", "false")
			buildConfigField("Boolean", "DEBUG_FEATURES", "false")
			isMinifyEnabled = true
			if (hasReleaseSigningConfig) {
				signingConfig = signingConfigs.getByName("release")
			}
			proguardFiles(
				getDefaultProguardFile("proguard-android-optimize.txt"),
				"proguard-rules.pro"
			)
		}
	}
	compileOptions {
		sourceCompatibility = JavaVersion.VERSION_17
		targetCompatibility = JavaVersion.VERSION_17
	}
	kotlinOptions {
		jvmTarget = "17"
	}
	buildFeatures {
		compose = true
		buildConfig = true
	}

	packaging {
		resources.excludes += "/META-INF/AL2.0"
		resources.excludes += "/META-INF/LGPL2.1"
	}
	namespace = "com.serranoie.app.minus"
}

dependencies {
	implementation(project(":sync-contract"))

	implementation(libs.androidx.core.ktx)
	implementation(libs.androidx.lifecycle.runtime.ktx)
	implementation("androidx.lifecycle:lifecycle-process:2.10.0")
	implementation(libs.androidx.activity.compose)
	implementation(platform(libs.androidx.compose.bom))
	implementation(libs.androidx.compose.ui)
	implementation(libs.androidx.compose.ui.graphics)
	implementation(libs.androidx.compose.ui.tooling.preview)
	implementation(libs.androidx.compose.runtime)
	implementation("androidx.compose.runtime:runtime-saveable")
	implementation(libs.androidx.compose.material)
	implementation(libs.androidx.compose.material.icons.extended)
	implementation(libs.androidx.wear.compose.material)
	implementation(libs.androidx.ui.graphics)
	implementation(libs.androidx.compose.foundation.layout)
	implementation(libs.ui.graphics)
	implementation(libs.androidx.foundation)
	testImplementation(libs.junit)
	androidTestImplementation(libs.androidx.junit)
	androidTestImplementation(libs.androidx.espresso.core)
	androidTestImplementation(platform(libs.androidx.compose.bom))
	androidTestImplementation(libs.androidx.compose.ui.test.junit4)
	debugImplementation(libs.androidx.compose.ui.tooling)
	debugImplementation(libs.androidx.compose.ui.test.manifest)

	implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2")
	implementation("androidx.compose.foundation:foundation:1.8.3")
	implementation("androidx.compose.foundation:foundation-layout:1.8.3")
	implementation("androidx.compose.ui:ui-util:1.8.3")
	implementation("androidx.compose.material3:material3:1.5.0-alpha17")
	implementation("androidx.compose.material3:material3-window-size-class:1.5.0-alpha14")
	implementation("androidx.compose.animation:animation:1.10.6")
	implementation("androidx.compose.ui:ui-tooling-preview:1.10.6")
	implementation("androidx.datastore:datastore-preferences:1.1.7")
	implementation("androidx.recyclerview:recyclerview:1.4.0")
	implementation("androidx.room:room-runtime:2.7.2")
	implementation("androidx.room:room-ktx:2.7.2")
	implementation("androidx.room:room-paging:2.7.2")
	implementation("androidx.hilt:hilt-navigation-compose:1.2.0")
	implementation("androidx.navigation:navigation-compose:2.7.7")
	implementation("com.google.android.gms:play-services-wearable:19.0.0")
	implementation("org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.10.2")
	implementation("androidx.lifecycle:lifecycle-viewmodel-compose:2.9.2")
	implementation("androidx.glance:glance-appwidget:1.1.1")
	implementation("androidx.glance:glance-appwidget-preview:1.1.1")
	implementation("androidx.glance:glance-preview:1.1.1")
	implementation("androidx.core:core-splashscreen:1.0.1")
	implementation("com.google.accompanist:accompanist-systemuicontroller:0.36.0")
	implementation("com.google.dagger:dagger:2.57")
	implementation("com.google.dagger:hilt-android:2.57")
	implementation("org.apache.commons:commons-csv:1.14.0")
	implementation("io.coil-kt:coil-compose:2.7.0")
	ksp("androidx.room:room-compiler:2.7.2")
	ksp("androidx.hilt:hilt-compiler:1.2.0")
	ksp("com.google.dagger:dagger-compiler:2.57")
	ksp("com.google.dagger:hilt-android-compiler:2.57")

	// Glance
	implementation("androidx.glance:glance-appwidget:1.1.1")
	implementation("androidx.glance:glance-material3:1.1.1")

	// WorkManager for notifications
	implementation("androidx.work:work-runtime-ktx:2.10.0")
	implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.3")
	implementation("androidx.hilt:hilt-work:1.2.0")
	ksp("androidx.hilt:hilt-compiler:1.2.0")

	// Logcat from Square
	implementation("com.squareup.logcat:logcat:0.4")

	debugImplementation("androidx.compose.ui:ui-tooling:1.10.6")
	debugImplementation("androidx.compose.ui:ui-test-manifest:1.8.3")
}
